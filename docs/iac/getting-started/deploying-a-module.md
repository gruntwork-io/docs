# Deploying your first module

Modules allow you to define an interface to create one or many resources in the cloud or on-premise. Similar to how in object oriented programming you can define a class that may have different attribute values across many instances.

Modules help keep your Terraform code DRY (don't repeat yourself), and speed up development time when creating new resources.

This tutorial will teach you how to develop a Terraform module that deploys an AWS Lambda. We will create the required file structure, define an AWS Lambda and AWS IAM role as code, then plan and apply the resource in an AWS account. Then, weâ€™ll verify the deployment by invoking the Lambda using the AWS CLI.

## Prerequisites
- An AWS account with permissions to create the necessary resources.
- An [AWS Identity and Access Management](https://aws.amazon.com/iam/) (IAM) user or role with permissions to create IAM roles and AWS Lambdas
- [AWS Command Line Interface](https://aws.amazon.com/cli/) (AWS CLI) installed on your local machine
- [Terraform](https://www.terraform.io) installed on your local machine

## Create the module

In this section you'll create a Terraform module that can create an AWS Lambda and AWS IAM role. This module will include three files - `main.tf` which will contain the resource definitions, `variables.tf`, which specifies the possible inputs to the module, and `outputs.tf`, which specifies the values that can be used to pass references to attributes from the resources in the module.

This module could be referenced many times to create any number of AWS Lambdas and IAM roles.


### Create a basic file structure
First, create the directories and files that will contain the Terraform configuration.

```sh
mkdir -p terraform-aws-gw-lambda-tutorial/modules/lambda
touch terraform-aws-gw-lambda-tutorial/modules/lambda/main.tf
touch terraform-aws-gw-lambda-tutorial/modules/lambda/variables.tf
touch terraform-aws-gw-lambda-tutorial/modules/lambda/outputs.tf
```

### Define the module resources

First, define the resources that should be created by the module. This is where you define resource level blocks provided by Terraform. For this module, we need an AWS Lambda and an AWS IAM role that will be used by the Lambda.

Paste the following snippet in `terraform-aws-gw-lambda/modules/lambda/main.tf`.
```hcl
resource "aws_iam_role" "lambda_role" {
  name = "${var.lambda_name}-role"

  assume_role_policy = <<EOF
{
  "Version": "2012-10-17",
  "Statement": [
    {
      "Action": "sts:AssumeRole",
      "Principal": {
        "Service": "lambda.amazonaws.com"
      },
      "Effect": "Allow",
      "Sid": ""
    }
  ]
}
EOF
}

data "archive_file" "zip" {
  type        = "zip"

  source {
    content  = templatefile(var.template_path, {})
    filename = var.output_file_name
  }

  output_path = "${path.module}/${var.lambda_name}.zip"
}

resource "aws_lambda_function" "lambda" {
  function_name = var.lambda_name
  handler       = var.handler
  filename      = data.archive_file.zip.output_path
  runtime       = var.runtime
  memory_size   = var.memory_size
  timeout       = var.timeout

  role = aws_iam_role.lambda_role.arn
}
```

### Specify the variables for the module

Now that you've defined the resources you want to create, you need to list out all of the variables that you want to allow users to pass into the variable. In the module, you can reference these values in the module using the `var` syntax, as visible in `terraform-aws-gw-lambda/modules/lambda/main.tf`.

Copy the following snippet into `terraform-aws-gw-lambda-tutorial/modules/lambda/variables.tf`.

```tf
variable "lambda_name" {
  type        = string
  description = "Name that will be used for the AWS Lambda"
}

variable "handler" {
  type        = string
  description = "The name of the handler function that will be called as the entrypoint of the lambda"
}

variable "template_path" {
  type        = string
  description = "The path to the template file to be deployed to lambda. Will be zipped!"
}

variable "output_file_name" {
  type        = string
  description = "The name of the file generated by the template"
}

variable "runtime" {
  type        = string
  description = "The runtime of the Lambda. Options include go, python, ruby, etc."
}

variable "memory_size" {
  type        = number
  description = "The amount of memory, in MB, to give to the Lambda. Defaults to 128."
  default     = 128
}

variable "timeout" {
  type        = number
  description = "The amount of time, in seconds, that a lambda can execute before timing out. Defaults to 30."
  default     = 30
}

```

### Specify the outputs

Terraform allows you to specify values that will be outputted by the module. Outputs are convenient ways to pass values between modules when composing a service comprised of many modules.

Copy the following snippet into `terraform-aws-gw-lambda-tutorial/modules/lambda/outputs.tf`.
```tf
output "function_name" {
  value = aws_lambda_function.lambda.function_name
}
```

## Reference the module

Now that you have defined a module that creates an AWS Lambda and AWS IAM Role, you can use the module to create the resources in AWS.

### Create the basic file structure

Now that you have the module defined, you need to create files which will reference the module. Typically, you would create a module in one repository, then reference it in a different repository. For this tutorial, we'll just create the reference in the top level directory for the sake of simplicity.

Create a `main.tf`, which will contain a reference to the module, and a file called `lambda.tftpl` which will contain a template for the Lambda function code.
```sh
touch terraform-aws-gw-lambda-tutorial/main.tf
touch terraform-aws-gw-lambda-tutorial/lambda.tftpl
```

### Define the function code template

Next, we'll create the template for the AWS Lambda. We define a simple Python function for the entrypoint that returns a string. The module will render this template into a `.py` file, then create a zip file that will be uploaded to the Lambda.

This template is being used as a convenient way to get simple code uploaded to Lambda.
```
def lambda_handler(event, context):
    return "Hello from Gruntwork!"
```

### Reference the module

Next, create a reference the module you just created in `terraform-aws-gw-lambda/modules/main.tf`. This code uses the `module` type from Terraform, which references the `modules/lambda` directory using the `source` attribute. You can then specify values for the required variables specified in `/modules/lambdas/variables.tf`. Finally, we specify an output using the value of the `module.lambda.function_name` output created in `/modules/lambdas/outputs.tf`

```
terraform {
  required_providers {
    aws = {
      source  = "hashicorp/aws"
      version = ">= 4.0.0"
    }
  }
}

module "lambda" {
  source = "./modules/lambda"

  lambda_name      = "gruntwork-lambda-tutorial"
  output_file_name = "main.py"
  handler          = "main.lambda_handler"
  template_path    = "${path.module}/lambda.tftpl"
  runtime          = "python3.9"
}

output "function_name" {
  value = module.lambda.function_name
}
```

## Plan and apply the module

### Run Terraform plan

Terraform will generate an execution plan using the `plan` action. The plan will show what resources Terraform determines need to be created or modified. The resources are determined based on the resources already existing in the lockfile (or the lack of existence of) and the current state of the deployed resources (e.g., an AWS Lambda). For example, if you applied the module from this guide with only the IAM role defined, then added the AWS Lambda, the subsequent plan would show that only the Lambda needs to be created.

Running `terraform plan` is helpful when developing modules, to confirm that the Terraform code you are writing, and to confirm what resources will be created or modified when applying the module in your AWS account.


From the `terraform-aws-gw-lambda-tutorial` directory, run a plan to see what resources will be created.
```sh
terraform plan
```

Review the output of `terraform plan`, it should contain two resources - an AWS Lambda and an AWS IAM role.


### Run Terraform apply

Terraform creates resources when using the `apply` action in a directory containing Terraform configuration files. Like with the `plan` command, Terraform will determine which resources need to be created or modified based on the resources already existing in the lockfile (or the lack of existence of the resource in the lockfile) and the current state of the deployed resources.

From the `terraform-aws-gw-lambda-tutorial` directory, run `terraform apply`. Terraform will pause to show you the resources it will create and prompt you to confirm resource creation.

```sh
terraform apply
```

Review the output to confirm it will only create an AWS Lambda and IAM role. Then, enter `yes` to confirm resource creation. Terraform will create the resources in your AWS account. Once complete, you can invoke the AWS Lambda following the steps in the next section.

## Invoke the created resource

Next, invoke the AWS Lambda to verify it was created and executing the application code.

Use `terraform output` to retreive the name of the AWS Lambda we provisioned. This uses the outputs we added to the module in [create a module](./deploying-a-module.md#create-a-module) to retrieve the name of the Lambda from Terraform state. Then, invoke the Lambda directly using the AWS CLI, writing the response of the Lambda to a file called `lambda_output`.
```sh
#!/bin/bash
export FUNCTION_NAME=$(terraform output -raw function_name)
aws lambda invoke --function-name $FUNCTION_NAME --output json lambda_output
```

The lambda invoke command should return JSON blob in response with the StatusCode of 200 and the ExecutedVersion of $LATEST.
```sh
{
    "StatusCode": 200,
    "ExecutedVersion": "$LATEST"
}
```

Inspect the contents of the `lambda_output` file, you should see a string stating `Hello from Gruntwork!`.

## Clean up

When you've completed the tutorial, clean up the resources you created to avoid incurring unexpected costs.

First, execute the `terraform plan -destroy` command to show the AWS resources that will be destroyed.
```sh
terraform plan -destroy
```

Review the output, it should should two resources to be destroyed - an AWS Lambda and IAM role and state the two resources will be destroyed.

Next, execute the `destroy` command.

```sh
terraform destroy
```

Finally, when prompted, enter `yes` to confirm the resource deletion. Terraform will begin destroying the resources created as part of this tutorial.


## What's next

Now that you've developed and deployed your first Terraform module, try creating another module that leverages the AWS Lambda Module you just created. For example, an AWS API Gateway HTTP API with an AWS Lambda integration. Alternatively, write a test using [Terratest](https://terratest.gruntwork.io/) that confirms your module creates resources as you'd expect.

Finally, consider what other resources you would create to make your modules ready to use in production. You would likely need to add logs, metrics, and additional AWS IAM policies to grant the Lambda access to other AWS resources such as Amazon Cloudwatch, DynamoDB, or SQS.

In [Using a module](../usage/using-a-module.md), we'll create the same resources defined in your modules using a pre-built Gruntwork module.


<!-- ##DOCS-SOURCER-START
{
  "sourcePlugin": "local-copier",
  "hash": "db6df562af70c49095c6f1a44457bca4"
}
##DOCS-SOURCER-END -->
